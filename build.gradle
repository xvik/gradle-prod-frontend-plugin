plugins {
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'net.researchgate.release' version '3.0.2'
    id 'ru.vyarus.quality' version '5.0.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'ru.vyarus.java-lib' version '2.4.0'
    id 'ru.vyarus.github-info' version '2.0.0'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.12"
}

// because of closure-stylesheets compiled for 11
sourceCompatibility = '11'

wrapper {
    gradleVersion = '7.6'
}

repositories { mavenLocal(); mavenCentral(); gradlePluginPortal() }
dependencies {
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.8.3'

    implementation 'org.jsoup:jsoup:1.17.2'
    implementation 'in.wilsonl.minifyhtml:minify-html:0.15.0'
    implementation 'commons-io:commons-io:2.15.1'
    implementation 'com.google.javascript:closure-compiler:v20231112'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation 'org.graalvm.js:js:22.3.5'

    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
}

group = 'ru.vyarus'
description = 'Optimize static resources for production'

github {
    user 'xvik'
    license 'MIT'
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// skip signing for jitpack (snapshots)
tasks.withType(Sign) {onlyIf { !System.getenv('JITPACK') }}

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

javaLib {
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
}


gradlePlugin {
    plugins {
        productionFrontendPlugin {
            id = 'ru.vyarus.prod-frontend'
            displayName = project.description
            description = 'Optimize static resources for production'
            tags.set(['html', 'js', 'css', 'frontend', 'minify', 'optimization'])
            implementationClass = 'ru.vyarus.gradle.frontend.ProdFrontendPlugin'
        }
    }
}

release.git.requireBranch.set('master')  // default is main

afterReleaseBuild {
    dependsOn = [
            'publishMavenPublicationToSonatypeRepository',
            'closeAndReleaseSonatypeStagingRepository',
            publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
    doLast {
        sleep(1000)
    }
}

dependencyUpdates.revision = 'release'
